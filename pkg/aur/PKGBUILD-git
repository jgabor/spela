# Maintainer: Jonathan Gabor <jonathan@jgabor.se>
pkgname=spela-git
pkgver=r1.abc1234
pkgrel=1
pkgdesc="Linux gaming optimization tool for NVIDIA GPUs with DLSS/DLL management (git version)"
arch=('x86_64')
url="https://github.com/jgabor/spela"
license=('MIT')
depends=('nvidia-utils' 'webkit2gtk-4.1' 'gtk3')
makedepends=('go>=1.22' 'bun' 'git')
optdepends=(
    'nvidia-settings: GPU settings control'
    'polkit: Privileged operations support'
    'ludusavi: Game save backup integration'
)
provides=('spela')
conflicts=('spela')
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
    cd spela
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd spela

    cd internal/gui/frontend
    bun install
    bun run build
    cd ../../..

    export CGO_ENABLED=1
    go build -tags production -ldflags="-s -w -X main.version=$pkgver" -o spela ./cmd/spela
}

package() {
    cd spela
    install -Dm755 spela "$pkgdir/usr/bin/spela"
    install -Dm644 data/polkit/io.github.spela.policy "$pkgdir/usr/share/polkit-1/actions/io.github.spela.policy"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
